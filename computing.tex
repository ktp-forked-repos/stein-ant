\chapter{Computing}
\section{Algorithms for Algebraic Number Theory}
The main algorithmic goals in algebraic number theory 
are to solve the following problems quickly:
\begin{itemize}
\item {\bf Ring of integers:} Given a number field $K$, specified
by an irreducible polynomial with coefficients in $\Q$, 
compute the ring of integers $\O_K$.
\item  {\bf Decomposition of primes:} Given a prime number 
$p\in\Z$, find the decomposition of the ideal $p\O_K$ as a product 
of prime ideals of $\O_K$.
\item {\bf  Class group:} Compute the class group of $K$, i.e., the
group of equivalence classes of nonzero ideals of $\O_K$, where $I$ and $J$
are equivalent if there exists $\alpha \in \O_K$
such that $IJ^{-1}=(\alpha)$.
\item  {\bf Units:} Compute generators for the group $U_K$ 
of  units of $\O_K$.
\end{itemize}
This chapter is about how to compute the first two using a computer.
\begin{center}
{\bf\Large This chapter will be completely rewritten.}
\end{center}

%As we will see, somewhat surprisingly it turns out that
%algorithmically by far the most time-consuming step in computing the
%ring of integers $\O_K$ seems to be to factor the discriminant of a
%polynomial whose root generates the field~$K$.  The algorithm(s) for
%computing $\O_K$ are quite complicated to describe, but the first step
%is to factor this discriminant, and it takes much longer in practice
%than all the other complicated steps.

The best overall reference for algorithms for doing basic algebraic
number theory computations is \cite{cohen:course_ant}.  This chapter
is not about algorithms for solving the above problems; instead is a
tour of the two most popular programs for doing algebraic number
theory computations, \magma{} and PARI.
These programs are both available to use via the web page
\begin{center}
\verb|http://modular.fas.harvard.edu/calc|
\end{center}

The following two sections illustrate what we've done so far in this
book, and a little of where we are going.  First we describe
\magma{} then PARI.

\section{\magma{}}
This section is a first introduction to \magma{} for algebraic number
theory.  \magma{} is a general purpose package for doing algebraic
number theory computations, but it is closed source and not free.  Its
development and maintenance at the University of Sydney is paid for by
grants and subscriptions.  I have visited Sydney three times to work
with them, and I also wrote the modular forms parts of MAGMA.

The documentation for \magma{} is available here:
\begin{center}
\verb|http://magma.maths.usyd.edu.au/magma/htmlhelp/MAGMA.htm|
\end{center}
Much of the algebraic number theory documentation is here:
\begin{center}
\verb|http://magma.maths.usyd.edu.au/magma/htmlhelp/text711.htm|
\end{center}

\subsection{Smith Normal Form}
In Section~\ref{sec:fg} we learned about Smith normal forms
of matrices.  
\begin{verbatim}
> A := Matrix(2,2,[1,2,3,4]);
> A;
[1 2]
[3 4]
> SmithForm(A);
[1 0]
[0 2]

[ 1  0]
[-1  1]

[-1  2]
[ 1 -1]
\end{verbatim}
As you can see, \magma{} computed the Smith form, which is 
$\abcd{1}{0}{0}{2}$.  What are the other two matrices
it output?  To see what any \magma{} command does, type
the command by itself with no arguments followed by a
semicolon.
\begin{verbatim}
> SmithForm;
Intrinsic 'SmithForm'

Signatures:

    (<Mtrx> X) -> Mtrx, AlgMatElt, AlgMatElt
    [
        k: RngIntElt, 
        NormType: MonStgElt, 
        Partial: BoolElt, 
        RightInverse: BoolElt
    ]

        The smith form S of X, together with unimodular matrices 
        P and Q such that P * X * Q = S.
\end{verbatim}
{\tt SmithForm} returns three arguments, a matrix and
matrices $P$ and $Q$ that transform the input matrix to Smith normal
form.  The syntax to ``receive'' three return arguments is natural,
but uncommon in other programming languages:
\begin{verbatim}
> S, P, Q := SmithForm(A);
> S;
[1 0]
[0 2]
> P;
[ 1  0]
[-1  1]
> Q;
[-1  2]
[ 1 -1]
> P*A*Q;
[1 0]
[0 2]
\end{verbatim}
Next, let's test the limits.  We make a $10\times 10$ 
integer matrix with random entries between $0$ and $100$,
and compute its Smith normal form.
\begin{verbatim}
> A := Matrix(10,10,[Random(100) : i in [1..100]]);
> time B := SmithForm(A);  
Time: 0.000
\end{verbatim}
Let's print the first row of $A$, the first and last
row of $B$, and the diagonal of $B$:
\begin{verbatim}
> A[1];
( 4 48 84  3 58 61 53 26  9  5)
> B[1];
(1 0 0 0 0 0 0 0 0 0)
> B[10];
(0 0 0 0 0 0 0 0 0 51805501538039733)
> [B[i,i] : i in [1..10]];
[ 1, 1, 1, 1, 1, 1, 1, 1, 1, 51805501538039733 ]
\end{verbatim}

Let's see how big we have to make~$A$ in order
to slow down \magma{} V2.11-10. 
 These timings below are on
an Opteron 248 server. 

\begin{verbatim}
> n := 50; A := Matrix(n,n,[Random(100) : i in [1..n^2]]);
> time B := SmithForm(A);
Time: 0.020
> n := 100; A := Matrix(n,n,[Random(100) : i in [1..n^2]]);
> time B := SmithForm(A);
Time: 0.210
> n := 150; A := Matrix(n,n,[Random(100) : i in [1..n^2]]);
> time B := SmithForm(A);
Time: 1.240
> n := 200; A := Matrix(n,n,[Random(100) : i in [1..n^2]]);
> time B := SmithForm(A);
Time: 4.920
\end{verbatim}
\begin{remark}
The same timings on a 1.8Ghz Pentium M notebook are 
{\tt 0.030, 0.410, 2.910, 10.600}, respectively, so about
twice as long.
On a G5 XServe (with Magma V2.11-2), they are 
{\tt 0.060, 0.640, 3.460, 12.270}, respectively, which
is nearly three times as long as the Opteron
(MAGMA seems very poorly optimized for the G5, so watch out).
\end{remark}

\begin{comment}
\magma{} can also work with finitely generated abelian groups.
\begin{verbatim}
> G := AbelianGroup([3,5,18]);
> G;
Abelian Group isomorphic to Z/3 + Z/90
Defined on 3 generators
Relations:
    3*G.1 = 0
    5*G.2 = 0
    18*G.3 = 0
> #G;
270
> H := sub<G | [G.1+G.2]>;
> #H;
15
> G/H;
Abelian Group isomorphic to Z/18
\end{verbatim}
\end{comment}

\subsection{Number Fields}
\begin{comment}
\magma{} has many commands for doing basic arithmetic with
$\Qbar$.
\begin{verbatim}
> Qbar := AlgebraicClosure(RationalField());
> Qbar;
> S<x> := PolynomialRing(Qbar);
> r := Roots(x^3-2);
> r;
[
    <r1, 1>,
    <r2, 1>,
    <r3, 1>
]
> a := r[1][1];
> MinimalPolynomial(a);
x^3 - 2
> s := Roots(x^2-7);
> b := s[1][1];
> MinimalPolynomial(b);
x^2 - 7
> a+b;
r4 + r1
> MinimalPolynomial(a+b);
x^6 - 21*x^4 - 4*x^3 + 147*x^2 - 84*x - 339
> Trace(a+b);
0
> Norm(a+b);
-339
\end{verbatim}
There are few commands for general algebraic number fields,
so usually we work in specific finitely generated subfields:
\begin{verbatim}
> MinimalPolynomial(a+b);
x^6 - 21*x^4 - 4*x^3 + 147*x^2 - 84*x - 339

> K := NumberField($1) ;  // $1 = result of previous computation.
> K;
Number Field with defining polynomial x^6 - 21*x^4 - 4*x^3 + 
    147*x^2 - 84*x - 339 over the Rational Field
\end{verbatim}
\end{comment}

To define a number field, we first define the polynomial ring
over the rational numbers.  The notation 
{\tt R<x>} below means ``the variable {\tt x} is the generator
of the polynomial ring''.  We then pass an irreducible polynomial
ot the {\tt NumberField} function.
\begin{verbatim}
> R<x> := PolynomialRing(RationalField());
> K<a> := NumberField(x^3-2);   // a is the image of x in Q[x]/(x^3-2)
> a;
a
> a^3;
2
\end{verbatim}

\subsection{Relative Extensions}
If $K$ is a number field, and $f(x)\in K[x]$ is an irreducible polynomial,
and $\alpha$ is a root of $f$, then $L=K(\alpha)\isom K[x]/(f)$ 
is a {\em relative extension} of $K$.  \magma{} can compute with
relative extensions, and also find the corresponding absolute
extension of $\Q$, i.e., find a polynomial $g$ such that
$K[x]/(f) \isom \Q[x]/(g)$.

The following illustrates defining
$L=K(\sqrt{a})$, where $K=\Q(a)$ and $a=\sqrt[3]{2}$.
\begin{verbatim}
> R<x> := PolynomialRing(RationalField());
> K<a> := NumberField(x^3-2);  
> S<y> := PolynomialRing(K);
> L<b> := NumberField(y^2-a);
> L;
Number Field with defining polynomial y^2 - a over K
> b^2;
a
> b^6;
2
> AbsoluteField(L);
Number Field with defining polynomial x^6 - 2 over the Rational 
Field
\end{verbatim}

\subsection{Rings of integers}
\magma{} can compute rings of integers of number fields.

\begin{verbatim}
> R<x> := PolynomialRing(RationalField());
> K<a> := NumberField(x^3-2);   // a is the image of x in Q[x]/(x^3-2)
> RingOfIntegers(K);
Maximal Equation Order with defining polynomial x^3 - 2 over ZZ
\end{verbatim}
Sometimes the ring of integers of $\Q(a)$ is not $\Z[a]$.
First a simple example, then a more complicated one:
\begin{verbatim}
> K<a> := NumberField(2*x^2-3);   // doesn't have to be monic
> 2*a^2 - 3;
0
> K;
Number Field with defining polynomial x^2 - 3/2 over the Rational
Field
> O := RingOfIntegers(K);
> O;
Maximal Order of Equation Order with defining polynomial 2*x^2 - 
    3 over ZZ
\end{verbatim}
Printing $\O_K$ gave us no real information.  Instead we
request a basis for $\O_K$:
\begin{verbatim}
> Basis(O);
[
    O.1,
    O.2
]
\end{verbatim}
Again we get no information.   To get a basis for $\O_K$ in
terms of $a=\sqrt{3/2}$, we use \magma's coercion operator {\tt !}:
\begin{verbatim}
> [K!x : x in Basis(O)];
[
    1,
    2*a       
]
\end{verbatim}
Thus the ring of integers has basis $1$ and 
$2\sqrt{3/2} = \sqrt{6}$ as a $\Z$-module.

Here are some more examples, which we've reformated for publication.
\begin{verbatim}
> procedure ints(f)   // (procedures don't return anything; functions do)
      K<a> := NumberField(f);
      O := RingOfIntegers(K);
      print [K!z : z in Basis(O)];
  end procedure;
> ints(x^2-5);
[
    1,  1/2*(a + 1)
]
> ints(x^2+5);
[
    1,  a
]
> ints(x^3-17);
[
    1,  a,  1/3*(a^2 + 2*a + 1)
]
> ints(CyclotomicPolynomial(7));  
[
    1, a, a^2, a^3, a^4, a^5
]
> ints(x^5+&+[Random(10)*x^i : i in [0..4]]);  // RANDOM
[
    1, a, a^2, a^3, a^4
]
> ints(x^5+&+[Random(10)*x^i : i in [0..4]]);  // RANDOM
[
    1, a, a^2,  1/2*(a^3 + a),
    1/16*(a^4 + 7*a^3 + 11*a^2 + 7*a + 14)
]
\end{verbatim}
Lets find out how high of a degree \magma{} can easily deal with.
\begin{verbatim}
> d := 10; time ints(&+[i*x^i + 2*x+1: i in [0..d]]);
[
    1, 10*a, ...
]
Time: 0.030
> d := 15; time ints(&+[i*x^i + 2*x+1: i in [0..d]]);
...
Time: 0.160
> d := 20; time ints(&+[i*x^i + 2*x+1: i in [0..d]]);
...
Time: 1.610
> d := 21; time ints(&+[i*x^i + 2*x+1: i in [0..d]]);
...
Time: 0.640
> d := 22; time ints(&+[i*x^i + 2*x+1: i in [0..d]]);
...
Time: 3.510
> d := 23; time ints(&+[i*x^i + 2*x+1: i in [0..d]]);
...
Time: 12.020
> d := 24; time ints(&+[i*x^i + 2*x+1: i in [0..d]]);
...
Time: 34.480
> d := 24; time ints(&+[i*x^i + 2*x+1: i in [0..d]]);
...
Time: 5.580 -- the timings very *drastically* on the same problem,
because presumably some randomized algorithms are used.
> d := 25; time ints(&+[i*x^i + 2*x+1: i in [0..d]]);
...
Time: 70.350
> d := 30; time ints(&+[i*x^i + 2*x+1: i in [0..d]]);
Time: 136.740
\end{verbatim}

Recall that an order is a subring of $\O_K$ of finite index as
an additive group.
We can also define orders in rings of integers in \magma.
\begin{verbatim}
> R<x> := PolynomialRing(RationalField());
> K<a> := NumberField(x^3-2);
> O := Order([2*a]);
> O;
Transformation of Order over 
Equation Order with defining polynomial x^3 - 2 over ZZ
Transformation Matrix:
[1 0 0]
[0 2 0]
[0 0 4]
> OK := RingOfIntegers(K);
> Index(OK,O);
8
\end{verbatim}
%> Discriminant(O);
%-6912
%> Discriminant(OK);
%-108
%> 6912/108;
%64    // perfect square...

\subsection{Ideals}
We can construct ideals in rings of integers of number fields
in \magma{} as illustrated. 
\begin{verbatim}
> R<x> := PolynomialRing(RationalField());
> K<a> := NumberField(x^2-5);
> OK := RingOfIntegers(K);
> I := 7*OK;
> I;
Principal Ideal of OK
Generator:
    [7, 0]
> J := (OK!a)*OK;    // the ! computes the natural image of a in OK
> J;
Principal Ideal of OK
Generator:
    [-1, 2]
> Generators(J);
[    [-1, 2]  ]
> K!Generators(J)[1];
a
> I*J;
Principal Ideal of OK
Generator:
    [-7, 14]
> J*I;
Principal Ideal of OK
Generator:
    [-7, 14]
> I+J;
Principal Ideal of OK
Generator:
    [1, 0]
> 
> Factorization(I);
[
    <Principal Prime Ideal of OK
    Generator:
        [7, 0], 1>
]
> Factorization(3*OK);
[
    <Principal Prime Ideal of OK
    Generator:
        [3, 0], 1>
]
> Factorization(5*OK);
[
    <Prime Ideal of OK
    Two element generators:
        [5, 0]
        [4, 2], 2>
]
> Factorization(11*OK);
[
    <Prime Ideal of OK
    Two element generators:
        [11, 0]
        [14, 2], 1>,
    <Prime Ideal of OK
    Two element generators:
        [11, 0]
        [17, 2], 1>
]
\end{verbatim}
We can even work with fractional ideals in \magma{}.
\begin{verbatim}
> K<a> := NumberField(x^2-5);
> OK := RingOfIntegers(K);
> I := 7*OK;
> J := (OK!a)*OK;
> M := I/J;
> M;
Fractional Principal Ideal of OK
Generator:
    -7/5*OK.1 + 14/5*OK.2
> Factorization(M);
[
    <Prime Ideal of OK
    Two element generators:
        [5, 0]
        [4, 2], -1>,
    <Principal Prime Ideal of OK
    Generator:
        [7, 0], 1>
]
\end{verbatim}

%In the next chapter, we will learn about discriminants and an
%algorithm for ``factoring primes'', that is writing an ideal $p\O_K$
%as a product of prime ideals of~$\O_K$.

\section{Using PARI}

PARI is freely available (under the GPL) from
\begin{center}
\verb|http://pari.math.u-bordeaux.fr/|
\end{center}
The above website describes PARI thus:
\begin{quote}
  PARI/GP is a widely used computer algebra system designed for fast
  computations in number theory (factorizations, algebraic number
  theory, elliptic curves...), but also contains a large number of
  other useful functions to compute with mathematical entities such as
  matrices, polynomials, power series, algebraic numbers etc., and a
  lot of transcendental functions. PARI is also available as a C
  library to allow for faster computations.

  Originally developed by Henri Cohen and his co-workers (Université
  Bordeaux I, France), PARI is now under the GPL and maintained by
  Karim Belabas (Université Paris XI, France) with the help of many
  volunteer contributors.
\end{quote}

The sections below are very similar to the \magma{} sections
above, except they address PARI instead of \magma{}.  We use
Pari Version 2.2.9-alpha for all examples below, and all timings
are on an Opteron 248.

\subsection{Smith Normal Form}
In Section~\ref{sec:fg} we learned about Smith normal forms
of matrices.   We create matrices in PARI by giving the
entries of each  row separated by a {\tt ;}.
\begin{verbatim}
? A = [1,2;3,4];
? A
[1 2]
[3 4]
? matsnf(A)
[2, 1]
\end{verbatim}
The {\tt matsnf} function computes the diagonal entries of the
Smith normal form of a matrix.  To get documentation about
a function PARI, type {\tt ?} followed by the function name.
\begin{verbatim}
? ?matsnf
matsnf(x,{flag=0}): Smith normal form (i.e. elementary divisors) 
of the matrix x, expressed as a vector d. Binary digits of flag 
mean 1: returns [u,v,d] where d=u*x*v, otherwise only the diagonal d 
        is returned, 
     2: allow polynomial entries, otherwise assume x is integral, 
     4: removes all information corresponding to entries equal 
        to 1 in d.
\end{verbatim}

Next, let's test the limits.  
To time code in PARI use the {\tt gettime} function:
\begin{verbatim}
? ?gettime
gettime(): time (in milliseconds) since last call to gettime.
\end{verbatim}
If we divide the result of {\tt gettime} by 1000 we get the time in seconds.

We make a $10\times 10$ 
integer matrix with random entries between $0$ and $100$,
and compute its Smith normal form.
\begin{verbatim}
> n=10;A=matrix(n,n,i,j,random(101));gettime;B=matsnf(A);gettime/1000.0
%7 = 0.0010000000000000000000000000000000000000
\end{verbatim}
Let's see how big we have to make~$A$ in order
to slow down PARI 2.2.9-alpha.
These timings below are on an Opteron 248 server. 
\begin{verbatim}
? n=50;A=matrix(n,n,i,j,random(101));gettime;B=matsnf(A);gettime/1000.0
0.058
? n=100;A=matrix(n,n,i,j,random(101));gettime;B=matsnf(A);gettime/1000.0
1.3920000000000000000000000000000000000
? n=150;A=matrix(n,n,i,j,random(101));gettime;B=matsnf(A);gettime/1000.0
30.731000000000000000000000000000000000
? n=200;A=matrix(n,n,i,j,random(101));gettime;B=matsnf(A);gettime/1000.0
  *** matsnf: the PARI stack overflows !
  current stack size: 8000000 (7.629 Mbytes)
  [hint] you can increase GP stack with allocatemem()
? allocatemem(); allocatemem(); allocatemem()
  *** allocatemem: Warning: doubling stack size; new stack = 16000000 (15.259 Mbytes).
? n=200;A=matrix(n,n,i,j,random(101));gettime;B=matsnf(A);gettime/1000.0
35.742000000000000000000000000000000000
\end{verbatim}
%6 = 229.58900000000000000000000000000000000
\begin{remark}
The same timings on a 1.8Ghz Pentium M notebook are 
{\tt 0.189, 5.489, 48.185, 170.21}, respectively.
On a G5 XServe, they are 
{\tt 0.19, 5.70, 41.95, 153.98}, respectively.

Recall that the timings for the same computation on the Opteron under \magma{}
are {\tt 0.020, 0.210, 1.240, 4.290}, which is {\em vastly} faster than PARI.
\end{remark}


\subsection{Number Fields}
There are several ways to define number fields in PARI.  The simplest is to
give a monic integral polynomial as input to the {\tt nfinit} function.
\begin{verbatim}
? K = nfinit(x^3-2);
\end{verbatim}
Number fields do not print as nicely in PARI as in \magma{}:
\begin{verbatim}
? K
K
%12 = [x^3 - 2, [1, 1], -108, 1, [[1, 1.2599210498948731647672106072782283506,
 ... and tons more numbers! ...
\end{verbatim}
Confusingly, elements of number fields can be represented in PARI in many different ways. 
I refer you to the documentation for PARI (\S3.6).  A simple way is as polymods:
\begin{verbatim}
? a = Mod(x, x^3-2)  \\ think of this as x in Q[x]/(x^3-2).
? a
Mod(x, x^3 - 2)
? a^3
Mod(2, x^3 - 2)
\end{verbatim}

\subsection{Relative Extensions}
[[Write this section.]]

\subsection{Rings of integers}
To compute the ring of integers of a number field in PARI, 
use the {\tt nfbasis} command.
\begin{verbatim}
? ?nfbasis
nfbasis(x,{flag=0},{p}): integral basis of the field Q[a], where a is 
a root of the polynomial x, using the round 4 algorithm. Second and
third args are optional.   Binary digits of flag means:
   1: assume that no square of a prime>primelimit divides the 
      discriminant of x, 
   2: use round 2 algorithm instead. 
If present, p provides the matrix of a partial factorization of the 
discriminant of x, useful if one wants only an order maximal at 
certain primes only.
\end{verbatim}

For example, we compute the ring of integers of $\Q(\sqrt[3]{2})$.
\begin{verbatim}
? nfbasis(x^3-2)
[1, x, x^2]
? nfbasis(2*x^2-3)
[1, 2*x]
\end{verbatim}

Here are some more examples, which we've reformated for publication.
\begin{verbatim}
> nfbasis(x^2-5)
[1, 1/2*x + 1/2]
? nfbasis(x^3-17)
[1, x, 1/3*x^2 - 1/3*x + 1/3]
? nfbasis(polcyclo(7))
[1, x, x^2, x^3, x^4, x^5]

? d=10; f=sum(i=0,d,i*x^i+2*x+1); gettime; nfbasis(f); gettime/1000.0
0.011000000000000000000000000000000000000
? d=15; f=sum(i=0,d,i*x^i+2*x+1); gettime; nfbasis(f); gettime/1000.0
0.027000000000000000000000000000000000000
? d=20; f=sum(i=0,d,i*x^i+2*x+1); gettime; nfbasis(f); gettime/1000.0
1.6690000000000000000000000000000000000
? d=20; f=sum(i=0,d,i*x^i+2*x+1); gettime; nfbasis(f); gettime/1000.0
1.6640000000000000000000000000000000000
? d=21; f=sum(i=0,d,i*x^i+2*x+1); gettime; nfbasis(f); gettime/1000.0
0.080000000000000000000000000000000000000
? d=22; f=sum(i=0,d,i*x^i+2*x+1); gettime; nfbasis(f); gettime/1000.0
1.5000000000000000000000000000000000000
? d=23; f=sum(i=0,d,i*x^i+2*x+1); gettime; nfbasis(f); gettime/1000.0
12.288000000000000000000000000000000000
? d=24; f=sum(i=0,d,i*x^i+2*x+1); gettime; nfbasis(f); gettime/1000.0
1.6380000000000000000000000000000000000
? d=25; f=sum(i=0,d,i*x^i+2*x+1); gettime; nfbasis(f); gettime/1000.0
38.141000000000000000000000000000000000
? d=30; f=sum(i=0,d,i*x^i+2*x+1); gettime; nfbasis(f); gettime/1000.0
33.423000000000000000000000000000000000
? d=30; f=sum(i=0,d,i*x^i+2*x+1); gettime; nfbasis(f); gettime/1000.0
33.066000000000000000000000000000000000  \\ doesn't vary much
\end{verbatim}
Lets find out how high of a degree PARI can easily deal with.
\begin{verbatim}
\end{verbatim}

Recall that the timing in \magma{} for computing $\O_K$ for
$d=30$ was 136.740 seconds, which is four times as long as the
33 second timing of PARI.\footnote{I onced discussed 
timings of PARI versus \magma{} with John Cannon, who is
the director of \magma{}.
He commented that in some cases PARI implicitly assumes unproven conjectures,
e.g., the Riemann Hypothesis,
in order to get such fast algorithms, whereas the philosophy in \magma{} 
is to not assume conjectures unless one explicitly asks it to.}
  Thus for the fields
considered above, computation of $\O_K$ in PARI is faster
than in MAGMA. Sometimes one system is {\em much}
better than another, and it is not clear a priori which will be
better.

\subsection{Ideals}
In PARI ideals can be represented in several ways.   For example, to construct
a principal ideal, give the generator.  There is no ideal data type, but you 
can give data that defines an ideal, e.g., an alement to define a principal ideal,
to functions that take ideals as input.

The following examples illustrate 
basic arithmetic with ideals and factorization of ideals.   
\begin{verbatim}
? a = Mod(x, x^2-5)
%41 = Mod(x, x^2 - 5)
? nf = nfinit(x^2-5);
? idealadd(nf, 5*a, 10*a)
%44 =
[25 15]
[ 0  5]
\end{verbatim}
The output of {\tt idealadd} is a $2$-column matrix, whose columns
represent elements of $K$ on the integral basis for $\O_K$ output by
{\tt nfbasis}.  These two elements generate the ideal as an
$\O_K$-module.  We will prove later (see Prop.~\ref{prop:2gen}) that
every ideal can be generated by $2$ elements.

Note that fractional ideals are also allowed:
\begin{verbatim}? idealadd(nf, a/5, 10*a)
%45 =
[1 3/5]
[0 1/5]
\end{verbatim}

We can also factor an ideal as a product of prime ideals.  The output
is an arrays of pairs, a prime ideal and its exponent.  These ideals
are represented in yet another way, as a $5$-tuple $[p,a,e,f,b]$, where
$a$ is a vector of integers.
Such a tuple corresponds to the ideal $p\O_K + \alpha \O_K$, where 
$\alpha = \sum a_i \omega_i$, and the $\omega_i$ are the basis
output by {\tt nfbasis}.  Explaining $e,f,b$ requires ideas that
we have not yet introduced (ramification, inertia degrees, etc.)
Here are some examples:
\begin{verbatim}
? nf = nfinit(x^2-5);
? idealfactor(nf, 7)
%46 =
[[7, [7, 0]~, 1, 2, [1, 0]~] 1]

? idealfactor(nf, 3)
%47 =
[[3, [3, 0]~, 1, 2, [1, 0]~] 1]

? idealfactor(nf, 5)
%48 =
[[5, [1, 2]~, 2, 1, [1, 2]~] 2]

? idealfactor(nf, 11)
%49 =
[[11, [-3, 2]~, 1, 1, [5, 2]~] 1]
[[11, [5, 2]~, 1, 1, [-3, 2]~] 1]
\end{verbatim}
We can even factor fractional ideals:
\begin{verbatim}
? idealfactor(nf, 1/11)
%50 =
[[11, [-3, 2]~, 1, 1, [5, 2]~] -1]
[[11, [5, 2]~, 1, 1, [-3, 2]~] -1]
\end{verbatim}

%%% Local Variables: 
%%% mode: latex
%%% TeX-master: "ant"
%%% End: 
